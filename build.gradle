plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.1.10'
    id 'edu.sc.seis.launch4j' version '3.0.6'
}

group 'org.example'
version '1.0'

repositories {
    mavenCentral()
    maven {
        url 'https://repo.repsy.io/mvn/njoh/public'
    }
}

dependencies {
    implementation 'no.njoh:pulse-engine:0.10.1'
}

kotlin {
    jvmToolchain(23)
}

////////////////////////////////////////////////////////////////////////////////// Windows release

String releaseName     = "$name-$version"
String releaseBuildDir = "$buildDir/$releaseName"
String mainClass       = 'GameTemplateKt'

jar {
    duplicatesStrategy DuplicatesStrategy.INCLUDE
    from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    exclude 'macos/**', 'linux/**' // Exclude natives for Mac and Linux when creating Windows exe
    manifest { attributes('Main-Class': mainClass) }
}

launch4j {
    bundledJrePath = 'jre'
    mainClassName  = mainClass
    outputDir      = releaseBuildDir
}

tasks.register('buildWin64Release', Zip) {
    group 'release'
    dependsOn 'createExe'
    doFirst {
        delete "$releaseBuildDir/lib"
        copy {
            from zipTree('jre/jre23-win64.zip')
            into "$releaseBuildDir/jre"
        }
    }
    from releaseBuildDir
    destinationDirectory.set(file('release/win64'))
    archiveFileName.set("${releaseName}.zip")
}